WHITESPACE = _{ " " | "\t" | "\n" | "\r" | COMMENT }
COMMENT    = _{ "#" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

input = { SOI ~ block* ~ EOI }
block = { ident ~ "{" ~ field* ~ "}" }

field =  { ident ~ ":" ~ value }
value = _{ text | int | float | bool | list }

text       = { "text" ~ "(" ~ string ~ ")" }
int        = { "int" ~ "(" ~ number ~ ")" }
float      = { "float" ~ "(" ~ number ~ "." ~ number? ~ ")" }
bool       = { "bool" ~ "(" ~ ident ~ ")" }
list       = { "list" ~ "(" ~ value_list? ~ ")" }
value_list = { value ~ ("," ~ value)* }

// basic types
ident  = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
number = @{ "-"? ~ ASCII_DIGIT+ }
